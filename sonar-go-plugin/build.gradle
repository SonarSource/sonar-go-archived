plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

// require uast-generator-go binaries to be build
shadowJar.dependsOn ':uast-generator-go:goBuild'
test.dependsOn ':uast-generator-go:goBuild'

dependencies {
    compile project(':common-rule-engine')
    // dependency on uast-generator-go binaries
    runtime files(project(':uast-generator-go').buildDir)

    compile     'com.google.code.gson:gson'
    compileOnly 'com.google.code.findbugs:jsr305'
    compile     'org.sonarsource.analyzer-commons:sonar-analyzer-commons'
    compile     'org.sonarsource.sonarqube:sonar-plugin-api'
    compile(group: 'org.sonarsource.slang', name: 'sonar-scala-plugin', version: '1.6.0.339')

    testCompile 'org.mockito:mockito-core'
    testCompile 'org.assertj:assertj-core'
    testCompile 'org.junit.jupiter:junit-jupiter-api'
    testCompile 'org.junit.jupiter:junit-jupiter-migrationsupport'
    testCompile 'org.awaitility:awaitility:3.1.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat 'full' // log the full stack trace (default is the 1st line of the stack trace)
        events "skipped", "failed" // verbose log for failed and skipped tests (by default the name of the tests are not logged)
    }
}

jar {
    manifest {
        def displayVersion = (project.buildNumber == null ? version : version.substring(0, version.lastIndexOf('.')) + " (build ${project.buildNumber})")
        def buildDate = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        attributes(
                'Build-Time': buildDate,
                'Implementation-Build': 'git rev-parse HEAD'.execute().text.trim(),
                'Plugin-BuildDate': buildDate,
                'Plugin-ChildFirstClassLoader': 'false',
                'Plugin-Class': 'org.sonar.go.plugin.GoPlugin',
                'Plugin-Description': 'Code Analyzer for Go',
                'Plugin-Developers': 'Alban Auzeill,Tibor Blenessy,Michael Gumowski,Janos Gyerik',
                'Plugin-Display-Version': displayVersion,
                'Plugin-Homepage': 'http://redirect.sonarsource.com/plugins/go.html',
                'Plugin-IssueTrackerUrl': 'https://github.com/SonarSource/sonar-go/issues',
                'Plugin-Key': 'go',
                'Plugin-License': 'GNU LGPL 3',
                'Plugin-Name': 'SonarGo',
                'Plugin-Organization': 'SonarSource',
                'Plugin-OrganizationUrl': 'http://www.sonarsource.com',
                'Plugin-SourcesUrl': 'https://github.com/SonarSource/sonar-go',
                'Plugin-Version': version,
                'Sonar-Version': '6.7',
                'SonarLint-Supported': 'false',
                'Version': "${version}",
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

shadowJar {
    exclude 'META-INF/**/*'
    dependencies {
        exclude(dependency('org.sonarsource.sonarqube:sonar-plugin-api'))
        exclude(dependency('org.codehaus.woodstox:.*'))
        exclude(dependency('org.codehaus.staxmate:.*'))
        exclude(dependency('com.google.code.findbugs:jsr305'))
    }
}

artifacts {
    archives shadowJar
}

artifactoryPublish.skip false

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: shadowJar, classifier: null
            if (release) {
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}
